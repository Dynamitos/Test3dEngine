#version 410 core

in vec3 normal_FS_in;
in vec2 texCoord_FS_in;
in vec3 worldPos_FS_in;
in vec3 cameraPos_FS_in;

out vec4 out_Color;

uniform mat4 transformationMatrix;
uniform sampler2D textureSampler;
uniform vec3 lightColor[4];
uniform vec3 attenuation[4];
uniform vec3 lightPosition[4];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColor;
uniform mat4 viewMatrix;
uniform float useFakeLighting;
uniform float numberOfRows;
uniform vec2 offset;
uniform float density = 0.000;
uniform float gradient = 0.0;

void main(void){
	
	vec4 worldPosition = vec4(worldPos_FS_in, 1);
	vec4 positionRelativeToCam = viewMatrix * worldPosition;
	vec3 actualNormal = normal_FS_in;
	if(useFakeLighting > 0.5){
		actualNormal = vec3(0, 1, 0);
	}
	
	float distanceCam = length(positionRelativeToCam.xyz);
	float visibility = exp(-pow((distanceCam * density),gradient));
	
	vec2 actualTex = (texCoord_FS_in/numberOfRows) + offset;
	vec4 textureColor = texture(textureSampler, actualTex);
	if(textureColor.a < 0.5){
		discard;
	}
	vec3 totalDiffuse = vec3(0.0);
	vec3 totalSpecular = vec3(0.0);
	vec3 unitNormal = normalize(actualNormal);
	vec3 unitVectorToCamera = normalize(cameraPos_FS_in - worldPos_FS_in);
	vec3 toLightVector[4];
	for(int i = 0; i < 4; i++){
		toLightVector[i] = lightPosition[i] - worldPosition.xyz;
		float distance = length(toLightVector[i]);
		float attFactor = attenuation[i].x + (attenuation[i].y * distance) +(attenuation[i].z * (distance * distance));
		vec3 unitLightVector = normalize(toLightVector[i]);
		float nDotl = dot(unitNormal, unitLightVector);
		float brightness = max(nDotl, 0.2);
		vec3 lightDirection = -unitLightVector;
		vec3 reflectedLightDirection = reflect(lightDirection,unitNormal);
		float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);
		specularFactor = max(specularFactor,0.0f);
		float dampedFactor = pow(specularFactor, shineDamper);
		totalDiffuse = totalDiffuse + (brightness * lightColor[i])/attFactor;
		totalSpecular = totalSpecular + (dampedFactor * reflectivity * lightColor[i])/attFactor;
	}
	totalDiffuse = max(totalDiffuse, 0.2);
	out_Color = vec4(totalDiffuse, 1.0f) * textureColor + vec4(totalSpecular, 1);
	out_Color = mix(vec4(skyColor,1.0), out_Color, visibility);
}