#version 410 core

in vec3 worldPos_FS_in;
in vec2 texCoord_FS_in;
in vec3 normal_FS_in;

out vec4 out_Color;

uniform sampler2D backgroundTexture;
uniform sampler2D rTexture;
uniform sampler2D gTexture;
uniform sampler2D bTexture;
uniform sampler2D blendMap;

uniform vec3 lightColor[4];
uniform vec3 lightPosition[4];
uniform vec3 attenuation[4];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColor;
uniform mat4 viewMatrix;
uniform float gradient = 0;
uniform float density = 0;
uniform vec3 cameraPos;

void main(void){
	
	vec4 blendMapColor = texture(blendMap, texCoord_FS_in);
	
	float backTextureAmount = 1 - (blendMapColor.r + blendMapColor.g + blendMapColor.b);
	vec2 tiledCoords = texCoord_FS_in * 40f;
	vec4 backgroundTextureColor = texture(backgroundTexture, tiledCoords) * backTextureAmount;
	vec4 rTextureColor = texture(rTexture,tiledCoords) * blendMapColor.r;
	vec4 gTextureColor = texture(gTexture,tiledCoords) * blendMapColor.g;
	vec4 bTextureColor = texture(bTexture,tiledCoords) * blendMapColor.b;
	
	vec4 totalColor = backgroundTextureColor + rTextureColor + gTextureColor + bTextureColor;
	
	vec3 unitNormal = normalize(normal_FS_in);
	vec3 unitVectorToCamera = normalize((cameraPos - worldPos_FS_in));
	vec3 totalDiffuse = vec3(0.0);
	vec3 totalSpecular = vec3(0.0);
	vec3 toLightVector[4];
	vec4 positionRelativeToCam = viewMatrix *  vec4(worldPos_FS_in, 1);
	float distance = length(positionRelativeToCam.xyz);
	float visibility = exp(-pow((distance * density),gradient));
	for(int i = 0; i < 4; i++){
		toLightVector[i] = lightPosition[i] - worldPos_FS_in;
		float distance = length(toLightVector[i]);
		float attFactor = attenuation[i].x + (attenuation[i].y * distance) +(attenuation[i].z * (distance * distance));
		vec3 unitLightVector = normalize(toLightVector[i]);
		float nDotl = dot(unitNormal, unitLightVector);
		float brightness = max(nDotl, 0.2);
		vec3 lightDirection = -unitLightVector;
		vec3 reflectedLightDirection = reflect(lightDirection,unitNormal);
		float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);
		specularFactor = max(specularFactor,0.0f);
		float dampedFactor = pow(specularFactor, shineDamper);
		totalDiffuse = totalDiffuse + (brightness * lightColor[i])/attFactor;
		totalSpecular = totalSpecular +  (dampedFactor * reflectivity * lightColor[i])/attFactor;
	}
	totalDiffuse = max(totalDiffuse, 0.2);
	out_Color = vec4(totalDiffuse, 1.0f) * totalColor + vec4(totalSpecular, 1);
	out_Color = mix(vec4(skyColor,1.0), out_Color, visibility);
}